apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    ext.addDependency = {
        task, flavor, dependency ->
            println('task=' + (String) task + 'flavor=' + (String) flavor + 'dependency=' + (String) dependency)
    }

    if (project.hasProperty("MyProject.properties")
            && project.hasProperty("PaidProject.properties")
            && new File(project.property("MyProject.properties") as String).exists()
            && new File(project.property("PaidProject.properties") as String).exists()) {

        Properties props = new Properties()

        signingConfigs {
            release {
                props.load(new FileInputStream(file(project.property("PaidProject.properties"))))
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
            }
            debug {
                props.load(new FileInputStream(file(project.property("MyProject.properties"))))
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
            }
        }
    }

    defaultConfig {
        applicationId "com.harlie.radiotheater.radiomysterytheater"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            shrinkResources false
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            useProguard true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'GOOGLE_PROJECT_NUMBER', MyGoogleProjectNumber
        it.buildConfigField 'String', 'GOOGLE_PROJECT_ID', MyGoogleProjectID
        it.buildConfigField 'String', 'TWITTER_API_KEY', MyTwitterAPIKey
        it.buildConfigField 'String', 'TWITTER_API_SECRET', MyTwitterAPISecret
    }

    productFlavors {
        paid {
            applicationId "com.harlie.radiotheater.radiomysterytheater.paid"
            versionName "1.0-Paid"
        }
        free {
            applicationId "com.harlie.radiotheater.radiomysterytheater"
            versionName "1.0-Free"
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.support:wearable:2.0.0-alpha1'
    compile 'com.google.android.gms:play-services-wearable:9.0.1'
    provided 'com.google.android.wearable:wearable:1.0.0'
}

