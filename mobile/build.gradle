buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'android-apt'
apply plugin: 'io.fabric'
apply from: '../preprocessor.gradle'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    ext.addDependency = {
        task, flavor, dependency ->
            println('task=' + (String) task + 'flavor=' + (String) flavor + 'dependency=' + (String) dependency)
    }

    if (project.hasProperty("MyProject.properties")
            && project.hasProperty("PaidProject.properties")
            && new File(project.property("MyProject.properties") as String).exists()
            && new File(project.property("PaidProject.properties") as String).exists()) {

        Properties props = new Properties()

        signingConfigs {
            release {
                props.load(new FileInputStream(file(project.property("PaidProject.properties"))))
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
            }
            debug {
                props.load(new FileInputStream(file(project.property("MyProject.properties"))))
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
            }
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = ["FABRIC_API_KEY": "ec24cad1b54e4429a75f75d2f7f8e49d4fb0fcfb"]
        multiDexEnabled true
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            useProguard false
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'SECRET_AUTH_PREFIX', MySecretAuthPrefix
        it.buildConfigField 'String', 'SECRET_PASS_SUFFIX', MySecretPassSuffix
        it.buildConfigField 'String', 'GOOGLE_PROJECT_NUMBER', MyGoogleProjectNumber
        it.buildConfigField 'String', 'GOOGLE_PROJECT_ID', MyGoogleProjectID
        it.buildConfigField 'String', 'GOOGLE_GCM_API_KEY', MyGoogleCLooudMessagingAPIKey
        it.buildConfigField 'String', 'GOOGLE_GCM_SENDER', MyGoogleCLooudMessagingSenderID
        it.buildConfigField 'String', 'FREE_TRACKING_ID', MyFreeTrackingID
        it.buildConfigField 'String', 'PAID_TRACKING_ID', MyPaidTrackingID
        it.buildConfigField 'String', 'TWITTER_API_KEY', MyTwitterAPIKey
        it.buildConfigField 'String', 'TWITTER_API_SECRET', MyTwitterAPISecret
        it.buildConfigField 'String', 'FABRIC_API_KEY', MyFabricAPIKey
    }

    productFlavors {
        paid {
            applicationId "com.harlie.radiotheater.radiomysterytheater.paid"
            versionName "1.0-Paid"
        }
        free {
            applicationId "com.harlie.radiotheater.radiomysterytheater"
            versionName "1.0-Free"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        disable 'InconsistentLayout','InvalidPackage','GradleDependency',
                'GradleDynamicVersion','PackageManagerGetSignatures','Recycle',
                'StaticFieldLeak','UnusedResources','IconLocation',
                'IconDensities','AlwaysShowAction','ClickableViewAccessibility',
                'SetTextI18n', 'UseCompoundDrawables', 'Overdraw'
    }

    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    wearApp project(':wear')
    compile('com.twitter.sdk.android:twitter:1.13.2@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.5.6@aar') {
        transitive = true;
    }

    compile 'com.google.firebase:firebase-core:9.2.0'
    compile 'com.google.firebase:firebase-database:9.2.0'
    compile 'com.google.firebase:firebase-auth:9.2.0'
    compile 'com.firebase:firebase-client-android:2.5.2+'
    compile 'com.firebaseui:firebase-ui:0.4.0'

    compile 'com.google.android.gms:play-services-cast:9.2.0'
    compile 'com.google.android.gms:play-services-auth:9.2.0'
    compile 'com.google.android.gms:play-services-plus:9.2.0'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:support-v4:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:support-annotations:24.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha3'

    freeCompile 'com.google.firebase:firebase-ads:9.2.0'

    compile 'com.github.jakob-grabner:Circle-Progress-View:v1.2.9'
    compile 'org.immutables:gson:2.2.6'

    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'

    compile 'com.google.android.support:wearable:1.4.0'
    compile 'com.android.support:mediarouter-v7:24.0.0'
    compile 'com.google.android.libraries.cast.companionlibrary:ccl:2.8.1'

    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'

    compile 'com.android.support:multidex:1.0.1'

    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:23.1.1'
    }
}

apply plugin: 'com.google.gms.google-services'
