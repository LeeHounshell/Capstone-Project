buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'android-apt'
apply plugin: 'io.fabric'
apply from: '../preprocessor.gradle'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = ["FABRIC_API_KEY": "ec24cad1b54e4429a75f75d2f7f8e49d4fb0fcfb"]
        multiDexEnabled true
    }

    ext.addDependency = {
        task, flavor, dependency ->
            println('task=' + (String) task + 'flavor=' + (String) flavor + 'dependency=' + (String) dependency)
    }

    signingConfigs {
        trial {
            keyAlias MyKeyAlias
            keyPassword MyKeyPassword
            storeFile file("../keystores-Radio-Mystery-Theater-FREE/lee.hounshell.keystore")
            storePassword MyStorePassword
        }
        paid {
            keyAlias MyKeyAlias
            keyPassword MyKeyPassword
            storeFile file("../keystores-Radio-Mystery-Theater-PAID/lee.hounshell.keystore")
            storePassword MyStorePassword
        }
    }

    productFlavors {
        paid {
            applicationId "com.harlie.radiotheater.radiomysterytheater.paid"
            versionName "1.0-Paid"
            signingConfig signingConfigs.paid
        }
        trial {
            applicationId "com.harlie.radiotheater.radiomysterytheater"
            versionName "1.0-Trial"
            signingConfig signingConfigs.trial
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            useProguard false
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'SECRET_AUTH_PREFIX', MySecretAuthPrefix
        it.buildConfigField 'String', 'SECRET_PASS_SUFFIX', MySecretPassSuffix
        it.buildConfigField 'String', 'FABRIC_API_KEY', MyFabricAPIKey
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        disable 'InconsistentLayout','InvalidPackage','GradleDependency',
                'GradleDynamicVersion','PackageManagerGetSignatures','Recycle',
                'StaticFieldLeak','UnusedResources','IconLocation',
                'IconDensities','AlwaysShowAction','ClickableViewAccessibility',
                'SetTextI18n', 'UseCompoundDrawables', 'Overdraw',
                'ExportedContentProvider', 'InefficientWeight'
    }

    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    wearApp project(':wear')
    compile('com.crashlytics.sdk.android:crashlytics:2.5.6@aar') {
        transitive = true;
    }

    compile 'com.google.firebase:firebase-core:9.2.0'
    compile 'com.google.firebase:firebase-database:9.2.0'
    compile 'com.google.firebase:firebase-auth:9.2.0'
    compile 'com.firebase:firebase-client-android:2.5.2+'
    compile 'com.firebaseui:firebase-ui:0.4.0'

    compile 'com.google.android.gms:play-services-cast:9.2.0'
    compile 'com.google.android.gms:play-services-auth:9.2.0'
    compile 'com.google.android.gms:play-services-plus:9.2.0'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:support-v4:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:support-annotations:24.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha4'

    trialCompile 'com.google.firebase:firebase-ads:9.2.0'

    compile 'com.github.jakob-grabner:Circle-Progress-View:v1.2.9'
    compile 'org.immutables:gson:2.2.6'

    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'

    compile 'com.google.android.support:wearable:1.4.0'
    compile 'com.android.support:mediarouter-v7:24.0.0'
    compile 'com.google.android.libraries.cast.companionlibrary:ccl:2.8.1'

    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'

    compile 'com.android.support:multidex:1.0.1'

    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:23.1.1'
    }
}

apply plugin: 'com.google.gms.google-services'
